
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//НачалоДоработки
	//Королёв Р.В.14.05.2024 
	//ДобавлениеРеквизитов
	KRV_ДобавитьРеквизитФормыПриСоздании();
	//КонецДоработки  
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	//НачалоДоработки
	//Королёв Р.В.14.05.2024 
	//Пересчет строки таб.части документа с учетом скидок
	
	СуммаСкидок = Объект.KRV_СогласованнаяСкидка + ТекущиеДанные.Скидка; 
	
	Если СуммаСкидок > 100 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Общая сумма скидок больше 100%, скорректировано системой!";
		Сообщение.Поле = "СогласованнаяСкидка";
		Сообщение.УстановитьДанные(Объект.KRV_СогласованнаяСкидка);
		Сообщение.Сообщить();
		СуммаСкидок = 100; 
		
	КонецЕсли;
	
	ТекущиеДанные.Сумма = (ТекущиеДанные.Цена - (ТекущиеДанные.Цена/100 * СуммаСкидок)) * ТекущиеДанные.Количество;	

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры
#Область KRV_Доработки

&НаСервере
Процедура KRV_ДобавитьРеквизитФормыПриСоздании()
	//НачалоДоработки
	//Королёв Р.В.14.05.2024 
	//Добавляет на форму новую команду и определяет действие
	
	НоваяКоманда = Команды.Добавить("KRV_ПересчитатьТабличныеЧасти");
	НоваяКоманда.Заголовок = "Пересчитать";
	НоваяКоманда.Отображение = ОтображениеКнопки.КартинкаИТекст;
	НоваяКоманда.Картинка = БиблиотекаКартинок.МетаданныеПерерасчеты;     
	НоваяКоманда.Действие = "KRV_ПересчитатьТабличныеЧасти"; 
	
	//КонецДоработки	
КонецПроцедуры 

&НаКлиенте 
Процедура KRV_ПересчитатьТабличныеЧасти(Команда)
	//НачалоДоработки
	//Королёв Р.В.14.05.2024 
	//Пересчет табличных частей документа с учетом скидки
	
	Для Каждого Товар Из Объект.Товары Цикл 
		РассчитатьСуммуСтроки(Товар);	
	КонецЦикла;   
	
	Для Каждого Услуга из Объект.Услуги Цикл    
		РассчитатьСуммуСтроки(Услуга);
	КонецЦикла;
	
	//КонецДоработки	
КонецПроцедуры 

//Элемент формы KRV_СогласованнаяСкидка добавлен програмно (KRV_РаботаСФормами)
//
&НаКлиенте 
Асинх Процедура KRV_СогласованнаяСкидкаПриИзменении(Элемент)
	//НачалоДоработки
	//Королёв Р.В.14.05.2024 
	//Предлагает пересчет табличных частей при изменении скидки
	
	ТекстВопроса = "Изменен процент скидки. Пересчитать таблицы товаров и услуг?";
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	KRV_ПересчитатьТабличныеЧасти(Элемент);
	
	//КонецДоработки
	КонецПроцедуры

#КонецОбласти
	
#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
